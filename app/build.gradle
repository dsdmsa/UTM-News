import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'


def final DEV = "dsdmsa.utmnews.dev"
def final PROD = "dsdmsa.utmnews.prod"
def final VERSION_NAME = "v0.2.${getGitCommitsNumber()}.${buildTime()}"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "dsdmsa.utmnews"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
//        vectorDrawables.useSupportLibrary = true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        dev {
            applicationId DEV
            signingConfig signingConfigs.debug
            versionName "dev.$VERSION_NAME"
        }

        prod {
            applicationId PROD
            versionName "prod.$VERSION_NAME"
            signingConfig signingConfigs.debug
        }

    }

    dexOptions {
        javaMaxHeapSize "4g"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:25.3.1"
    compile "com.android.support:recyclerview-v7:25.3.1"
    compile "com.android.support:design:25.3.1"
    compile "com.android.support:preference-v7:25.3.1"
    compile 'com.android.support:cardview-v7:25.3.1'

    //network
    compile 'com.google.code.gson:gson:2.8.0'
    compile "com.squareup.retrofit2:retrofit:2.2.0"
    compile "com.squareup.retrofit2:converter-gson:2.1.0"
    compile "com.squareup.okhttp3:okhttp:3.5.0"
    compile "com.squareup.okhttp3:logging-interceptor:3.5.0"

    // dependenci injeciton
    compile "com.google.dagger:dagger:2.9"
    annotationProcessor "com.google.dagger:dagger-compiler:2.9"
    provided 'javax.annotation:jsr250-api:1.0'
    // view injection
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    //image processer
    compile 'com.github.bumptech.glide:glide:3.7.0'
    //html parser
    compile 'org.jsoup:jsoup:1.10.2'
    // MVP framework
    compile 'com.arello-mobile:moxy-app-compat:1.4.6'
    annotationProcessor 'com.arello-mobile:moxy-compiler:1.4.6'
    // memory leak
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    //logger
    compile 'com.jakewharton.timber:timber:4.5.1'
    //bus
    compile 'org.greenrobot:eventbus:3.0.0'
    //costum chrome tab
    compile 'me.zhanghai.android.customtabshelper:library:1.0.3'
    //costum toasts
    compile 'com.github.GrenderG:Toasty:1.1.4'
    //keyboard control
    compile 'com.github.Commit451:Teleprinter:1.0.1'
    //database
    compile "android.arch.lifecycle:extensions:1.0.0-alpha1"
    compile "android.arch.persistence.room:runtime:1.0.0-alpha1"
    annotationProcessor "android.arch.lifecycle:compiler:1.0.0-alpha1"
    annotationProcessor "android.arch.persistence.room:compiler:1.0.0-alpha1"
    //reactive
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.0.1'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'


    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'

//    compile 'com.github.hackware1993:MagicIndicator:1.5.0'
    compile 'com.ogaclejapan.smarttablayout:library:1.6.1@aar'
    //testing
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}

static def buildTime() {
    def df = new SimpleDateFormat("yyyyMMdd")
    df.setTimeZone(TimeZone.getTimeZone("GMT"))
    return df.format(new Date())
}

def getGitCommitsNumber() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--all', '--count'
        standardOutput = stdout
    }
    return stdout.toString().replace('\n','')
}