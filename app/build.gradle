buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.24.1'
    }
}
import java.text.SimpleDateFormat

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def final DEV = "dsdmsa.utmnews.dev"
def final PROD = "dsdmsa.utmnews.prod"
def final VERSION_NAME = "v0.4.${getGitCommitsNumber()}.${buildTime()}"

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.2'
    defaultConfig {
        applicationId "dsdmsa.utmnews"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }

    flavorDimensions "app"

    productFlavors {
        dev {
            dimension "app"
            applicationId DEV
            signingConfig signingConfigs.debug
            versionName "dev.$VERSION_NAME"
        }

        prod {
            dimension "app"
            applicationId PROD
            versionName "prod.$VERSION_NAME"
            signingConfig signingConfigs.debug
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:preference-v7:27.0.2'
    implementation 'com.android.support:cardview-v7:27.0.2'
    implementation 'com.android.support:customtabs:27.0.2'

    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation 'com.google.dagger:dagger:2.12'
    implementation 'com.jakewharton:butterknife:8.8.1'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'org.jsoup:jsoup:1.10.2'
    implementation 'com.arello-mobile:moxy-app-compat:1.4.6'
    implementation 'com.jakewharton.timber:timber:4.5.1'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'com.github.GrenderG:Toasty:1.1.4'
    implementation 'com.github.Commit451:Teleprinter:1.0.1'
    implementation 'android.arch.lifecycle:extensions:1.0.0'
    implementation 'android.arch.persistence.room:runtime:1.0.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    implementation 'com.google.firebase:firebase-crash:11.8.0'
    implementation 'com.ogaclejapan.smarttablayout:library:1.6.1@aar'
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }

    annotationProcessor 'com.google.dagger:dagger-compiler:2.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    annotationProcessor 'com.arello-mobile:moxy-compiler:1.4.6'
    annotationProcessor 'android.arch.lifecycle:compiler:1.0.0'
    annotationProcessor 'android.arch.persistence.room:compiler:1.0.0'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.4'

    provided 'javax.annotation:jsr250-api:1.0'

    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

}

static def buildTime() {
    def df = new SimpleDateFormat("yyyyMMdd")
    df.setTimeZone(TimeZone.getTimeZone("GMT"))
    return df.format(new Date())
}

def getGitCommitsNumber() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--all', '--count'
        standardOutput = stdout
    }
    return stdout.toString().replace('\n', '')
}

apply plugin: 'com.google.gms.google-services'